from os.path import join

DATA_URL = 'https://static-content.springer.com/esm/art%3A10.1038%2Fnature05649/MediaObjects/41586_2007_BFnature05649_MOESM264_ESM.zip'
OUTPUT_DIR = 'output'
RAW_DIR = join(OUTPUT_DIR, 'raw')
RAW_DATA_ZIP = join(RAW_DIR,'collins2007_raw.zip')
RAW_SUPP_DIR = join(RAW_DIR, 'collins_et_al_s2')
RAW_SUPP_EMAP_TSV = join(RAW_SUPP_DIR, 'Chromosome biology EMAP data.txt')
RAW_SUPP_FUNCATS_XLS = join(RAW_SUPP_DIR, 'Summary of mutations and genetic profiles.xls')

PROCESSED_DIR = join(OUTPUT_DIR, 'processed')
PROCESSED_EMAP = join(PROCESSED_DIR, 'collins_et_al-dels_only.cpkl')
PROCESSED_FUNCATS = join(PROCESSED_DIR, 'collins_et_al_gene_annotations.tsv')

rule all:
    input:
        PROCESSED_EMAP,
        PROCESSED_FUNCATS,


rule supp_tsvs:
    input:
        xls = RAW_SUPP_FUNCATS_XLS,
        script = 'parse_collins.py'
    output:
        PROCESSED_FUNCATS
    shell:
        """
        python {input.script} xls-to-csv \
            --input_xls '{input.xls}' \
            --output {output} \
            --usecols 4 \
            --skiprows 3
        """

rule emap:   
    input:
        tsv=RAW_SUPP_EMAP_TSV,
        script= 'parse_collins.py'
    output:
        PROCESSED_EMAP
    shell:
        "python {input.script} parse-emap -i '{input.tsv}' -o {output}" #todo? rename the files so spaces aren't annoying?

rule unzip:
    input:
        RAW_DATA_ZIP
    params:
        unzipped_folder = "'Supp data - strains and interaction data'"
    output:
        RAW_SUPP_EMAP_TSV,
        RAW_SUPP_FUNCATS_XLS
    shell:
        '''
        unzip {input} && \
        mv {params.unzipped_folder}/* {RAW_SUPP_DIR}/. && \
        rmdir {params.unzipped_folder}
        '''
rule download_raw_data:
    params:
        url = DATA_URL
    output:
        RAW_DATA_ZIP
    shell:
        'wget -O {output} {params.url}' 

# ALL
