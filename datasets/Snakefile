from os.path import join

# Processed datasets
names = ['collins_et_al',
         'roguev_et_al',
         'costanzo_et_al_2010',
         'costanzo_et_al_2016']

output_dir = 'all_processed_datasets'
PROCESSED_DIR_FMT = '{name}/output/processed'
COLLECTED_DATASET_FMT = join(output_dir, '{name}')


# Rules
rule all:
    input:
        expand(COLLECTED_DATASET_FMT, name=names),
        join(output_dir, 'meta.txt'),
        join(output_dir, 'checksums.yml'),
        join(output_dir, 'verify_result.yml')

rule clean:
    shell:
        '''
        rm -r {output_dir}
        '''

rule meta:
    output:
        join(output_dir, 'meta.txt')
    shell:
        '''
        git log -1 > {output}
        '''

rule one_dataset:
    input:
        PROCESSED_DIR_FMT
    output:
        directory(COLLECTED_DATASET_FMT)
    shell:
        '''
        cp -r {input} {output}
        '''

# create md5 checksums
rule list_outputs:
    input:
        expand(COLLECTED_DATASET_FMT, name=names)
    output:
        'all_processed_files.txt'
    shell:
        '''
        echo outputs > {output}
        ls -1 */output/processed/* >> {output}
        ls -1 all_processed_datasets/*/* >> {output}
        '''

rule checksums:
    input:
        'all_processed_files.txt'
    params:
        'checksums.yml' # NB: do not make this an output, we want contract and verify to not depend on eachother....
    shell:
        'chacon create -i {input} -o {params}'

rule verify:
    input:
        'checksums.yml'
    output:
        result=join(output_dir, 'verify_result.yml'),
        checksums=join(output_dir, 'checksums.yml')
    shell:
        'chacon verify -i {input} > {output.result} && cp {input} {output.checksums}'
